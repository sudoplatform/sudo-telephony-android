apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.amazonaws.appsync'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'org.jetbrains.dokka'
apply plugin: "org.sonarqube"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled = true
//         TODO: The Android X Test Orchestrator is broken in Android Studio 3.6.2, will be fixed in 3.6.3
//        execution 'ANDROIDX_TEST_ORCHESTRATOR'

        unitTests.all {
            useJUnitPlatform()
        }
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
    }
}

dependencyCheck {
    suppressionFile file("../dependency-suppression.xml").toString()
    failBuildOnCVSS 0
}

ext {
    awsAndroidSdkVersion = "2.16.3"
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // AWS stuff
    implementation "com.amazonaws:aws-android-sdk-core:$awsAndroidSdkVersion"
    implementation("com.amazonaws:aws-android-sdk-appsync:$appSyncVersion")
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.2'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'

    // Sudo libs
    api "com.sudoplatform:sudoconfigmanager:[1.2.2,)"
    api "com.sudoplatform:sudokeymanager:[3.4.1,)"
    api "com.sudoplatform:sudoprofiles:[2.1.2,)"

    testImplementation "junit:junit:4.12"
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.0'
    testImplementation "org.mockito:mockito-core:2.16.0"
    testImplementation "org.robolectric:robolectric:4.2"
    testImplementation "org.awaitility:awaitility-kotlin:3.1.6"
    testImplementation("com.nhaarman:mockito-kotlin:1.5.0") {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }

    androidTestImplementation "org.awaitility:awaitility-kotlin:3.1.6"
    androidTestImplementation 'com.google.truth:truth:0.32'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestUtil 'androidx.test:orchestrator:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation "org.mockito:mockito-android:2.16.0"

}
repositories {
    mavenCentral()
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

apply plugin: 'maven'

ext {
    PUBLISH_GROUP_ID = 'com.sudoplatform'
    PUBLISH_ARTIFACT_ID = 'sudotelephony'
    PUBLISH_DESCRIPTION = 'Telephony SDK for the Sudo Platform by Anonyome Labs.'
    PUBLISH_PROJECT_URL = 'https://github.com/sudoplatform/sudo-telephony-android'
    PUBLISH_PROJECT_SCM = 'scm:git:github.com/sudoplatform/sudo-telephony-android.git'
    if (project.hasProperty('tag')) {
        PUBLISH_VERSION = "$tag"
    } else {
        PUBLISH_VERSION = "0.0.0"
    }
}

apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"
